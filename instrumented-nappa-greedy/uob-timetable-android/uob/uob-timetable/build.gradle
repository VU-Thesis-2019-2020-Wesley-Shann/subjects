apply plugin: 'com.android.application'

def gitRevParse = { arg ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', arg, 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return "Git error"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'
    defaultConfig {
        applicationId "baseline.com.ak.uobtimetable"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 21
        versionName "2.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "GIT_COMMIT_HASH", "\"${gitRevParse("--short")}\""
        buildConfigField "String", "GIT_BRANCH", "\"${gitRevParse("--abbrev-ref")}\""
        buildConfigField "String", "BUGSNAG_KEY", "\"${System.getenv('UOB_TIMETABLE_BUGSNAG')}\""
    }
    buildTypes {
        debug {
            buildConfigField "boolean", "IS_DEBUG", "true"
        }
        release {
            minifyEnabled false
            buildConfigField "boolean", "IS_DEBUG", "false"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.preference:preference:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.vectordrawable:vectordrawable:1.0.0'

    implementation 'com.squareup.okhttp3:okhttp:3.14.8'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.bugsnag:bugsnag-android:4.0.0'
    implementation 'org.apache.commons:commons-lang3:3.4'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'
    implementation 'com.h6ah4i.android.tablayouthelper:tablayouthelper:0.9.1'

    implementation name: 'nappa-experimentation', ext: 'aar'

    testImplementation 'junit:junit:4.12'
}
